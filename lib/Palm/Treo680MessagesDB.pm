# $Id: Treo680MessagesDB.pm,v 1.2 2008/07/03 21:10:48 drhyde Exp $

package Palm::Treo680MessagesDB;

use strict;
use warnings;

use Palm::Raw();
use DateTime;

use vars qw($VERSION @ISA $timezone $incl_raw $debug);

$VERSION = '1.1';
@ISA = qw(Palm::Raw);
$timezone = 'Europe/London';
$debug = 0;
$incl_raw = 0;

sub import {
    my $class = shift;
    my %opts = @_;
    $timezone = $opts{timezone} if(exists($opts{timezone}));
    $incl_raw = $opts{incl_raw} if(exists($opts{incl_raw}));
    $debug    = $opts{debug}    if(exists($opts{debug}));
    eval "use Data::Hexdumper ()" if($debug);
    Palm::PDB::RegisterPDBHandlers(__PACKAGE__, [MsSt => 'MsDb']);
}

=head1 NAME

Palm::Treo680MessagesDB - Handler for Treo 680 SMS message databases

=head1 SYNOPSIS

    use Palm::PDB;
    use Palm::Treo680MessagesDB timezone => 'Europe/London';

    my $pdb = Palm::PDB->new();
    $pdb->Load("MessagesDB.pdb");
    print Dumper(@{$pdb->{records}})'

=head1 DESCRIPTION

This is a helper class for the Palm::PDB package, which parses the
database generated by a Treo 680 as a record of all your SMSes.

=head1 OPTIONS

You can set some global options when you 'use' the module:

=over

=item timezone

Defaults to 'Europe/London'.

=item incl_raw

Whether to include the raw binary blob of data in the parsed records.
Only really useful for debuggering, and so defaults to false.

=item debug

Spit out some useful info for debugging and for reverse-engineering
unknown record types.

=back

=head1 METHODS

This class inherits from Palm::Raw, so has all of its methods.  The
folliwing are over-ridden, and differ from that in the parent class
thus:

=head2 ParseRecord

Returns data structures with the following keys:

=over

=item rawdata

The raw data blob passed to the method.  This is only present if the
incl_raw option is true.

=item date

The date of the message, if available, in YYYY-MM-DD format

=item time

The time of the message, if available, in HH:MM format

=item epoch or timestamp (it's available under both names)

The epoch time of the message, if available.  Note that because
the database doesn't
store the timezone, we assume 'Europe/London'.  If you want to change
that, then suppy a timezone option when you 'use' the module.

Internally, this uses the DateTime module.  In the case of
ambiguous times then it uses the latest UTC time.  For invalid
local times, the epoch is set to -1, an impossible number as it's
before Palm even existed.

Note that this is always the Unix epoch time.  See L<DateTime> for
details of what this means.

=item name

The name of the other party, which the Treo extracts from the SIM
phone-book or from the Palm address book at the time the SMS is saved.

=item number or phone

The number of the other party.  This is not normalised so you might see
the same number in different formats, eg 07979866975 and +447979866975.
I may add number normalisation in the future.

=item direction

Either 'incoming', or 'outgoing'.

=back

Other fields may be added in the future.

=cut

sub ParseRecord {
    my $self = shift;
    my %record = @_;

    my $buf = $record{rawdata} = delete($record{data});

    my $type = 256 * ord(substr($buf, 10, 1)) + ord(substr($buf, 11, 1));
    my($dir, $num, $name, $msg) = ('', '', '', '');
    if($type == 0x400C || $type == 0x4009) { # 4009 not used by 680?
        $dir = ($type == 0x400C) ? 'inbound' : 'outbound';
        ($num, $name, $msg) = (split(/\00+/, substr($buf, 34)))[0, 1, 3];
        $msg = substr($msg, 1);
    } elsif($type == 0) {
        $dir = 'outbound';
        ($num, $name, $msg) = split(/\00+/, substr($buf, 0x4C), 3);
        $msg =~ s/^.{9}//s;
        $msg =~ s/\00.*$//s;

    } elsif($type == 0x0002) {
        $dir = 'outbound';
        ($num, $name, $msg) = split(/\00+/, substr($buf, 0x46), 3);
        $msg =~ s/^.Trsm....//s;
        $msg =~ s/\00.*$//s;
    } else {
        printf("UNKNOWN TYPE: 0x%04X\n\n", $type) if($debug);
        $type = 'unknown';
    }
    if($type ne 'unknown') {
        print "Offset: $record{offset}\n" if($debug);
        printf("Type:   0x%04X\nDir:    %s\nNumber: %s\nName:   %s\nMsg:    %s\n", $type, $dir, $num, $name, $msg) if($debug);
        print "Buffer:\n".Data::Hexdumper::hexdump(data => $buf)
            if($debug == 2 || ($debug && !$msg));
        print "\n" if($debug);
    } elsif($debug) {
        print "Offset: $record{offset}\n";
        print "Buffer:\n".Data::Hexdumper::hexdump(data => $buf)."\n\n";
    }
    # $date  = 256 * ord($bytes[2]) + ord($bytes[3]);
    # $time  = 256 * ord($bytes[4]) + ord($bytes[5]);
    # my $year = 1904 + (($date & 0b1111111000000000) >> 9);
    # my $month = sprintf('%02d', ($date & 0b111100000) >> 5);
    # my $day = sprintf('%02d', $date & 0b11111);
    # my $hour = sprintf('%02d', $time >> 8);
    # my $minute = sprintf('%02d', $time & 255);
    # @record{qw(date time)} = ("$year-$month-$day", "$hour:$minute");
    # $record{epoch} = eval { DateTime->new(
    #     year => $year, month => $month, day => $day,
    #     hour => $hour, minute => $minute, time_zone => $timezone
    # )->epoch(); } || -1;
    delete $record{rawdata} unless($incl_raw);
    $record{device}    = 'Treo 680';
    $record{direction} = '';# inbound or outbound
    $record{phone}     = $record{number} = $num;
    $record{timestamp} = $record{epoch};
    $record{name}      = $name;
    $record{text}      = $msg;
    $record{type}      = $type;
    return \%record;
}

=head1 LIMITATIONS

The message format is undocumented.  Consequently it has had to be
reverse-engineered.  There appear to be several message formats in
the database, not all of which are handled.

There is currently no support for creating a new database, or for
editing the contents of an existing database.  If you need that
functionality, please submit a patch with tests.  I will *not* write
this myself unless I need it.

Behaviour if you try to create or edit a database is currently
undefined.

=head1 BUGS and FEEDBACK

I can only reverse-engineer record formats that appear on my phone, so
there may be some missing.  In addition, I may decode some formats
incorrectly because they're not quite what I thought they were.

If you find any bugs please report them either using
L<http://rt.cpan.org/> or by email.  Ideally, I would like to receive a
sample database and a test file, which fails with the latest version of
the module but will pass when I fix the bug.

=head1 SEE ALSO

L<Palm::SMS>, which handles SMS messages databases on some other models
of Treo, and includes very basic Treo 680 support.

L<Palm::PDB>

L<DateTime>

=head1 AUTHOR

David Cantrell E<lt>F<david@cantrell.org.uk>E<gt>

=head1 AUTHOR, COPYRIGHT and LICENCE

Copyright 2008 David Cantrell E<lt>david@cantrell.org.ukE<gt>

This software is free-as-in-speech software, and may be used,
distributed, and modified under the terms of either the GNU
General Public Licence version 2 or the Artistic Licence. It's
up to you which one you use. The full text of the licences can
be found in the files GPL2.txt and ARTISTIC.txt, respectively.

=head1 CONSPIRACY

This module is also free-as-in-mason software.

=cut

1;
